plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.elba.peachstore'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	bootJar.enabled = false
	jar.enabled = true

	group = 'com.elba.peachstore'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '1.8'

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

project(':interfaces') {
	bootJar.enabled = true
	bootJar.archiveFileName = "peach-app.jar"
	jar.enabled = false
	dependencies {
		implementation project(':application')
		implementation project(':domain')
		implementation project(':infrastructure')
		implementation 'org.springframework.boot:spring-boot-starter-web'
	}
}

project(':application') {
	dependencies {
		implementation project(':domain')
	}
}

project(':domain') {
	dependencies {
		implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	}
}

project(':infrastructure') {
	dependencies {
		implementation project(':application')
		implementation project(':domain')
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'com.h2database:h2'
		implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
